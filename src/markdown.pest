document = _{ SOI ~ section* ~ EOI }
section = _{ blank_line* ~ section_contents ~ blank_line* }
section_contents = _{ codeblock | header | bullet_list | ordered_list | verbatim | paragraph }

COMMENT = { ("[//]: # (" ~ ("\\)" | (!")" ~ ANY))* ~ ")") | "<!--" ~ (!"-->" ~ ANY)+ ~ "-->" }

space = _{ " " | "\t" }

blank_line = _{ space* ~ NEWLINE }
indent = _{ " "{4} | "\t" }
non_indent_space = _{ " "{1,3} }
unescaped_asterisk = _{ !"\\" ~ "*" }
unescaped_tilde = _{ !"\\" ~ "~" }
unescaped_backtick = _{ !"\\" ~ "`" }
unescaped_hash = _{ !"\\" ~ "#" }
unescaped_underscore = _{ !"\\" ~ "_" }

char = _{ ANY }
markup = _{ strong_markup | italic_markup | strikethrough_markup | inline_code_markup | underline_markup | image | link }
str = { (!(markup | endline | NEWLINE) ~ char)+ }

normal_endline = _{ space? ~ NEWLINE ~ !blank_line }
trailing_endline = _{ space* ~ NEWLINE ~ EOI }
linebreak = { "  " ~ normal_endline }
endline = _{ normal_endline | trailing_endline | linebreak }

inline = _{ (markup | str) }
inlines = _{ ((!endline ~ inline) | (endline ~ &inline))+ ~ endline? }

alt_text = { (!"]" ~ !NEWLINE ~ ANY)+ }
url = { (!(")" | ">") ~ !space ~ !NEWLINE ~ ANY)+ }

paragraph = { non_indent_space? ~ inlines ~ (blank_line+ | EOI) }
verbatim = { (indent+ | (non_indent_space? ~ ">" ~ space*)) ~ inlines }

header_hashes = { unescaped_hash ~ "#"{0,5} ~ !"#" }
header_title = { ((!NEWLINE ~ !space ~ ANY) | (space+ ~ !NEWLINE ~ &ANY))+ }
header = { non_indent_space? ~ header_hashes ~ space* ~ header_title ~ space* ~ endline? ~ (blank_line+ | EOI)}

codeblock_close = _{ "```" }
codeblock_open = _{ non_indent_space? ~ unescaped_backtick ~ "``" }
codeblock_lang = { ASCII_ALPHANUMERIC+ }
codeblock_body = { (!"```" ~ ((!NEWLINE ~ ANY) | (NEWLINE+ ~ space* ~ !codeblock_close)))+ }
codeblock = { codeblock_open ~ space* ~ #lang = codeblock_lang? ~ space* ~ NEWLINE ~ #body = codeblock_body ~ NEWLINE? ~ space* ~ codeblock_close }

end_list = { space* ~ COMMENT }

list_content_start = { inline ~ (NEWLINE ~ PEEK_ALL ~ !(bullet | enumerator | space) ~ inline)* }
list_content_continuation = ${ (!end_list ~ PEEK_ALL ~ (&space{2,} | !(bullet | enumerator)) ~ section_contents ~ blank_line*)+ }

bullet = _{ ("*" | "-" | "+") ~ space+ }
bullet_list_item = { !space ~ bullet ~ list_content_start? ~ blank_line* ~ list_content_continuation? }
bullet_list = ${ PUSH(space*) ~ bullet_list_item ~ (!end_list ~ PEEK_ALL ~ bullet_list_item)* ~ DROP }

enumerator = _{ ASCII_DIGIT{1,3} ~ ("." | ")") ~ space+ }
ordered_list_item = { !space ~ enumerator ~ list_content_start? ~ blank_line* ~ list_content_continuation? }
ordered_list = ${ PUSH(space*) ~ ordered_list_item ~ (!end_list ~ PEEK_ALL ~ ordered_list_item)* ~ DROP }

two_star_open = _{ unescaped_asterisk ~ "*" ~ !(space | NEWLINE) }
two_star_close = _{ !(space | NEWLINE) ~ unescaped_asterisk ~ "*" }
strong = { (!(NEWLINE | two_star_close) ~ ANY)+ }
strong_markup = _{ two_star_open ~ strong ~ two_star_close }

one_star_open = _{ unescaped_asterisk ~ !(space | NEWLINE) }
one_star_close = _{ !(space | NEWLINE) ~ unescaped_asterisk }
italic = { (!(NEWLINE | one_star_close) ~ ANY)+ }
italic_markup = _{ one_star_open ~ italic ~ one_star_close }

two_tilde_open = _{ unescaped_tilde ~ "~" ~ !(space | NEWLINE) }
two_tilde_close = _{ !(space | NEWLINE) ~ unescaped_tilde ~ "~" }
strikethrough = { (!(NEWLINE | two_tilde_close) ~ ANY)+ }
strikethrough_markup = _{ two_tilde_open ~ strikethrough ~ two_tilde_close }

one_backtick_open = _{ unescaped_backtick ~ !(space | NEWLINE) }
one_backtick_close = _{ !(space | NEWLINE) ~ unescaped_backtick }
inline_code = { (!( NEWLINE | one_backtick_close) ~ ANY)+ }
inline_code_markup = _{ one_backtick_open ~ inline_code ~ one_backtick_close }

one_underscore_open = _{ unescaped_underscore ~ !(space | NEWLINE) }
one_underscore_close = _{ !(space | NEWLINE) ~ unescaped_underscore }
underline = { (!(NEWLINE | one_underscore_close) ~ ANY)+ }
underline_markup = _{ one_underscore_open ~ underline ~ one_underscore_close }

directed_link = _{ "[" ~ #alt = alt_text ~ "](" ~ #url = url ~ ")" }
autolink = { "<" ~ #url = url ~ ">" }
link = { directed_link | autolink }

image = { space* ~ "![" ~ #alt = alt_text ~ "](" ~ #url = url ~ ")" }